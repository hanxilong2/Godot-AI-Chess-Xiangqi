extends InspectableItem
class_name Chessboard

signal clicked()
signal click_selection()
signal click_empty()
signal selection_down()
signal empty_down()
signal selection_up()
signal empty_up()
signal empty_double_click()
signal animation_finished()

@export var COLOR_LAST_MOVE:Color = Color(0.569, 0.569, 0.569, 1.0)
@export var COLOR_MOVE:Color = Color(0.56, 0.157, 0.164, 1.0)
@export var COLOR_POINTER:Color = Color(0.176, 0.176, 0.176, 1.0)
@export var actor_scale_factor:float = 1

var backup_piece:Array = []
var steady_piece:Dictionary = {}
var mouse_start_position_name:String = ""
var mouse_hold:bool = false
var mouse_moved:bool = false
var button_input_hold:bool = false
var button_input_moved:bool = false
var button_input_pointer:int = 0

var state:State = null
var chessboard_piece:Dictionary[int, Actor] = {}
var king_instance:Array[Actor] = [null, null]

var square_selection:int = 0
var selected:int = -1
var double_click_timer:float = 0

func _ready() -> void:
	super._ready()

func add_default_piece_set() -> void:
	backup_piece.clear()
	chessboard_piece.clear()
	for i:int in 128:
		if !state.has_piece(i):
			continue
		match String.chr(state.get_piece(i)):
			"K":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_king_white.tscn").instantiate(), i)
			"Q":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_queen_white.tscn").instantiate(), i)
			"R":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_rook_white.tscn").instantiate(), i)
			"B":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_bishop_white.tscn").instantiate(), i)
			"N":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_knight_white.tscn").instantiate(), i)
			"P":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_pawn_white.tscn").instantiate(), i)
			"k":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_king_black.tscn").instantiate(), i)
			"q":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_queen_black.tscn").instantiate(), i)
			"r":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_rook_black.tscn").instantiate(), i)
			"b":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_bishop_black.tscn").instantiate(), i)
			"n":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_knight_black.tscn").instantiate(), i)
			"p":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_pawn_black.tscn").instantiate(), i)
			"w":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_checker_1_black.tscn").instantiate(), i)
			'*':
				add_piece_instance(load("res://resource/chess/scene/actor/piece_checker_2_black.tscn").instantiate(), i)
			'#':
				add_piece_instance(load("res://resource/chess/scene/actor/piece_checker_3_black.tscn").instantiate(), i)
			"z":
				add_piece_instance(load("res://resource/chess/scene/actor/piece_checker_4_black.tscn").instantiate(), i)

func remove_piece_set() -> void:
	for by:int in chessboard_piece:
		chessboard_piece[by].queue_free()
	for iter:Actor in backup_piece:
		iter.queue_free()
	chessboard_piece.clear()
	backup_piece.clear()

func button_input(_button:String, _pressed:bool) -> void:
	var camera:Camera3D = get_viewport().get_camera_3d()
	var direction:float = camera.global_rotation.y
	direction -= global_rotation.y
	var direction_mapping:Dictionary = {}
	if direction > -PI / 4 * 3 && direction <= -PI / 4:
		direction_mapping = {"up": 1, "down": -1, "left": -16, "right": 16}
	elif direction > -PI / 4 && direction <=  PI / 4:
		direction_mapping = {"up": -16, "down":16, "left": -1, "right": 1}
	elif direction >  PI / 4 && direction <=  PI / 4 * 3:
		direction_mapping = {"up": -1, "down": 1, "left": 16, "right": -16}
	else:
		direction_mapping = {"up": 16, "down": -16, "left": 1, "right": -1}

	if _button in ["up", "down", "left", "right"]:
		if !_pressed:
			return
		button_input_moved = true
		if !((button_input_pointer + direction_mapping[_button]) & 0x88):
			button_input_pointer += direction_mapping[_button]
			finger_on_position(Chess.x88_to_name(button_input_pointer))
	elif _button =="accept":
		if _pressed:
			button_input_hold = true
			button_input_moved = false
			tap_position(Chess.x88_to_name(button_input_pointer), true)
		elif button_input_moved:
			tap_position(Chess.x88_to_name(button_input_pointer), false)
			button_input_moved = false
			button_input_hold = false
		else:
			button_input_hold = false

func area_input(_from:Node3D, _to:Area3D, _instant:bool, _pressed:bool, _event_position:Vector3, _normal:Vector3) -> void:
	if _instant:
		if _pressed:
			finger_on_position(_to.get_name())
			tap_position(_to.get_name(), true)
			mouse_hold = true
			mouse_moved = false
			mouse_start_position_name = _to.get_name()
			clicked.emit.call_deferred()
		elif mouse_moved:
			tap_position(_to.get_name(), false)
			finger_up()
			mouse_start_position_name = ""
			mouse_moved = false
			mouse_hold = false
		else:
			mouse_hold = false
	else:
		var position_name:String = _to.get_name()
		if mouse_start_position_name != position_name:
			mouse_moved = true
		finger_on_position(position_name)

func set_state(_state:State) -> void:
	$canvas.clear_pointer("last_move")
	$canvas.clear_pointer("move")
	state = _state.duplicate()
	#king_instance[0].set_warning(Chess.is_check(state, 1))
	#king_instance[1].set_warning(Chess.is_check(state, 0))

func x88_to_vector3(_by:int) -> Vector3:
	var position_name:String = "%c%d" % [_by % 16 + 97, 7 - _by / 16 + 1]
	return get_node(position_name).position

func vector3_to_x88(_position:Vector3) -> int:
	return Chess.name_to_x88(vector3_to_name(_position))

func vector3_to_name(_position:Vector3) -> String:
	var nearest:Area3D = null
	for i:int in 8:
		for j:int in 8:
			var position_name:String = "%c%d" % [i + 97, j + 1]
			if !nearest || _position.distance_squared_to(get_node(position_name).global_position) < _position.distance_squared_to(nearest.global_position):
				nearest = get_node(position_name)
	return nearest.name

func name_to_vector3(_position_name:String) -> Vector3:
	return get_node(_position_name).position

func tap_position(position_name:String, down:bool = true) -> void:
	selected = Chess.name_to_x88(position_name)
	if square_selection != -1 && (Chess.mask(Chess.x88_to_c64(selected)) & square_selection):
		if down:
			selection_down.emit.call_deferred()
		else:
			selection_up.emit.call_deferred()
		click_selection.emit.call_deferred()
		return
	click_empty.emit.call_deferred()
	if down:
		if (Time.get_unix_time_from_system() - double_click_timer <= 0.3):
			empty_double_click.emit.call_deferred()
			double_click_timer = 0
		else:
			double_click_timer = Time.get_unix_time_from_system()
		empty_down.emit.call_deferred()
	else:
		empty_up.emit.call_deferred()

func finger_on_position(position_name:String) -> void:
	$canvas.clear_pointer("pointer")
	if !position_name:
		return
	$canvas.draw_pointer("pointer", COLOR_POINTER, Chess.name_to_x88(position_name))

func finger_up() -> void:
	$canvas.clear_pointer("pointer")

func set_square_selection(_square_selection:int) -> void:
	$canvas.clear_pointer("move")
	square_selection = _square_selection
	var bit:int = square_selection
	while bit:
		var by:int = Chess.c64_to_x88(Chess.first_bit(bit))
		$canvas.draw_pointer("move", COLOR_MOVE, by)
		bit = Chess.next_bit(bit)

func execute_move(move:int) -> Dictionary:
	var event:Dictionary = Chess.apply_move_custom(state, move)
	var rollback_event:Dictionary = receive_event(event)
	Chess.apply_move(state, move)
	$canvas.clear_pointer("move")
	$canvas.clear_pointer("last_move")
	$canvas.draw_pointer("last_move", COLOR_LAST_MOVE, Chess.from(move))
	$canvas.draw_pointer("last_move", COLOR_LAST_MOVE, Chess.to(move))
	#king_instance[0].set_warning(Chess.is_check(state, 1))
	#king_instance[1].set_warning(Chess.is_check(state, 0))
	selected = -1
	return rollback_event

func receive_event(event:Dictionary) -> Dictionary:
	match event["type"]:
		"move":
			move_piece_instance(event["from"], event["to"])
			return event.duplicate()
		"capture":
			var captured_instance:Actor = chessboard_piece[event["to"]]
			capture_piece_instance(event["from"], event["to"])
			return {
				"type": "capture",
				"from": event["from"],
				"to": event["to"],
				"captured_instance": captured_instance
			}
		"promotion&capture":
			var captured_instance:Actor = chessboard_piece[event["to"]]
			promote_and_capture_piece_instance(event["from"], event["to"], event["piece"])
			return {
				"type": "promotion&capture",
				"from": event["from"],
				"to": event["to"],
				"captured_instance": captured_instance
			}
		"promotion":
			promote_piece_instance(event["from"], event["to"], event["piece"])
			return event.duplicate()
		"castle":
			castle_piece_instance(event["from_king"], event["to_king"], event["from_rook"], event["to_rook"])
			return event.duplicate()
		"en_passant":
			var captured_instance:Actor = chessboard_piece[event["captured"]]
			en_passant_piece_instance(event["from"], event["to"], event["captured"])
			return {
				"type": "en_passant",
				"captured_instance": captured_instance,
				"captured": event["captured"],
				"from": event["from"],
				"to": event["to"]
			}
		"introduce":
			move_piece_instance_from_steady(event["by"], event["piece"])
			return event.duplicate()
		"leave":
			move_piece_instance_to_steady(event["by"], event["piece"])
			return event.duplicate()
		"king_explore":
			king_explore_instance(event["from"], event["path"])
			return {
				"type": "king_explore",
				"from": event["from"],
				"to": event["path"][-1]
			}
		"pass":
			do_nothing()
			return event.duplicate()
	return {}

func receive_rollback_event(event:Dictionary) -> void:
	match event["type"]:
		"capture":
			move_piece_instance(event["to"], event["from"])
			move_piece_instance_from_backup(event["to"], event["captured_instance"])
		"promotion&capture":
			chessboard_piece[event["to"]].unpromote()
			move_piece_instance(event["to"], event["from"])
			move_piece_instance_from_backup(event["to"], event["captured_instance"])
		"promotion":
			chessboard_piece[event["to"]].unpromote()
			move_piece_instance(event["to"], event["from"])
		"move":
			move_piece_instance(event["to"], event["from"])
		"castle":
			castle_piece_instance(event["to_king"], event["from_king"], event["to_rook"], event["from_rook"])
		"en_passant":
			move_piece_instance(event["to"], event["from"])
			move_piece_instance_from_backup(event["captured"], event["captured_instance"])
		"introduce":
			move_piece_instance_to_steady(event["by"], event["piece"])
		"leave":
			move_piece_instance_from_steady(event["by"], event["piece"])
		"king_explore":
			move_piece_instance(event["to"], event["from"])
		"pass":
			do_nothing()

func add_piece_instance(instance:Actor, by:int) -> void:
	$pieces.add_child(instance)
	instance.scale *= actor_scale_factor
	if by == -1:
		instance.visible = false
		backup_piece.push_back(instance)
	else:
		instance.visible = true
		chessboard_piece[by] = instance
		if state.get_piece(by) == ord("K"):
			king_instance[0] = instance
		if state.get_piece(by) == ord("k"):
			king_instance[1] = instance
		instance.introduce(get_node(Chess.x88_to_name(by)).global_position)

func add_piece_instance_to_steady(instance:Actor, piece:int) -> void:
	steady_piece.get_or_add(piece, []).push_back(instance)
	$pieces.add_child(instance)
	instance.visible = false

func move_piece_instance_to_steady(by:int, piece:int) -> void:
	var instance:Actor = chessboard_piece[by]
	chessboard_piece.erase(by)
	steady_piece.get_or_add(piece, []).push_back(instance)
	instance.leave()
	await instance.animation_finished
	animation_finished.emit.call_deferred()

func move_piece_instance_from_steady(by:int, piece:int) -> void:
	var instance:Actor = steady_piece[piece][-1]
	steady_piece[piece].pop_back()
	chessboard_piece[by] = instance
	instance.introduce(get_node(Chess.x88_to_name(by)).global_position)
	await instance.animation_finished
	animation_finished.emit.call_deferred()

func remove_piece_instance(instance:Actor) -> void:
	var by:Variant = chessboard_piece.find_key(instance)
	$pieces.remove_child(instance)
	chessboard_piece.erase(by)
	backup_piece.erase(instance)

func move_piece_instance_to_other(from:int, to:int, other:Chessboard) -> Actor:
	var instance:Actor = chessboard_piece[from]
	chessboard_piece.erase(from)
	instance.get_parent().remove_child(instance)
	other.add_piece_instance(instance, to)
	return instance

func move_piece_instance_from_backup(by:int, instance:Actor) -> void:
	chessboard_piece[by] = instance
	backup_piece.erase(instance)
	instance.introduce(get_node(Chess.x88_to_name(by)).global_position)
	await instance.animation_finished
	animation_finished.emit.call_deferred()

func move_piece_instance(from:int, to:int) -> void:
	var instance:Actor = chessboard_piece[from]
	instance.move(get_node(Chess.x88_to_name(to)).global_position)
	chessboard_piece.erase(from)
	chessboard_piece[to] = instance
	await instance.animation_finished
	animation_finished.emit.call_deferred()

func castle_piece_instance(from_1:int, to_1:int, from_2:int, to_2:int) -> void:
	var instance_1:Actor = chessboard_piece[from_1]
	instance_1.move(get_node(Chess.x88_to_name(to_1)).global_position)
	chessboard_piece.erase(from_1)
	chessboard_piece[to_1] = instance_1
	var instance_2:Actor = chessboard_piece[from_2]
	instance_2.move(get_node(Chess.x88_to_name(to_2)).global_position)
	chessboard_piece.erase(from_2)
	chessboard_piece[to_2] = instance_2
	await instance_1.animation_finished
	animation_finished.emit.call_deferred()

func capture_piece_instance(from:int, to:int) -> void:
	var instance_from:Actor = chessboard_piece[from]
	var instance_to:Actor = chessboard_piece[to]
	instance_from.capturing(get_node(Chess.x88_to_name(to)).global_position, instance_to)
	move_piece_instance_to_backup(to)
	chessboard_piece.erase(from)
	chessboard_piece[to] = instance_from
	await instance_from.animation_finished
	animation_finished.emit.call_deferred()

func promote_piece_instance(from:int, to:int, piece:int) -> void:
	var instance:Actor = chessboard_piece[from]
	instance.promote(get_node(Chess.x88_to_name(to)).global_position, piece)
	chessboard_piece.erase(from)
	chessboard_piece[to] = instance
	await instance.animation_finished
	animation_finished.emit.call_deferred()

func promote_and_capture_piece_instance(from:int, to:int, piece:int) -> void:
	var instance_from:Actor = chessboard_piece[from]
	var instance_to:Actor = chessboard_piece[to]
	instance_from.capturing(get_node(Chess.x88_to_name(to)).global_position, instance_to)
	move_piece_instance_to_backup(to)
	await instance_from.animation_finished
	instance_from.promote(get_node(Chess.x88_to_name(to)).global_position, piece)
	await instance_from.animation_finished
	chessboard_piece.erase(from)
	chessboard_piece[to] = instance_from
	animation_finished.emit.call_deferred()

func en_passant_piece_instance(from:int, to:int, captured:int) -> void:
	var instance_from:Actor = chessboard_piece[from]
	chessboard_piece[from].capturing(get_node(Chess.x88_to_name(to)).global_position, chessboard_piece[captured])
	move_piece_instance_to_backup(captured)
	chessboard_piece.erase(from)
	chessboard_piece[to] = instance_from
	await instance_from.animation_finished
	animation_finished.emit.call_deferred()

func move_piece_instance_to_backup(by:int) -> void:
	var instance:Actor = chessboard_piece[by]
	chessboard_piece.erase(by)
	backup_piece.push_back(instance)
	#instance.visible = !pieces[instance]["hide_piece"]
	#instance.move(to_global(pieces[instance]["initial_position"]))

func leave_piece_instance(by:int, pos:Vector3) -> void:
	var instance:Actor = chessboard_piece[by]
	chessboard_piece.erase(by)
	instance.move(pos)
	await instance.animation_finished
	animation_finished.emit.call_deferred()

func king_explore_instance(from:int, path:PackedInt32Array) -> void:
	if path.is_empty():
		return
	var instance:Actor = chessboard_piece[from]
	chessboard_piece.erase(from)
	chessboard_piece[path[-1]] = instance
	for to:int in path:
		instance.move(get_node(Chess.x88_to_name(to)).global_position)
		await instance.animation_finished
	animation_finished.emit.call_deferred()

func do_nothing() -> void:
	# 鐢变簬鐗垫壇鍒板彂閫佷俊鍙凤紝绌虹潃涔熼渶瑕佸啓鍑芥暟
	animation_finished.emit.call_deferred()

func set_enabled(_enabled:bool) -> void:
	super.set_enabled(_enabled)
	if !enabled:
		$canvas.clear_pointer("move")
		$canvas.clear_pointer("pointer")
		selected = -1

func draw_pointer(type:String, color:Color, by:int, priority:int = 0) -> void:
	$canvas.draw_pointer(type, color, by, priority)

func clear_pointer(type:String) -> void:
	$canvas.clear_pointer(type)
